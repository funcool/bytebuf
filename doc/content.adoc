= octet - a library for working with byte buffers
funcool.org
1.1.2
:toc: left
:numbered:
:source-highlighter: pygments
:pygments-style: friendly
:sectlinks:

== Introduction

Working with raw bytes is tedious, and sometimes the language abstractions for
working with bytes is not very pleasant. _octet_ library offers a simple API for
clojure (jvm) and clojurescript (js) that makes working with a bytebuffer painless.

This is a short list of project goals:

- Not to be intrusive (no bytebuffer wrapping).
- Provide host independent abstraction (as much as possible).
- Composability.


=== Project Maturity

Since _octet_ is a young project, expect some API breakage.


=== Install

The simplest way to use _octet_ in a clojure project, is by including it in the
dependency vector on your *_project.clj_* file:

[source,clojure]
----
[funcool/octet "1.1.2"]
----

And the library works with the following platforms: *jdk7*, *jdk8*, *node-lts*.


== Getting started

The main goal of _octet_ is to provide a multiplatform abstraction for working with
byte buffers - offering a lightweight API for defining message types declaratively,
and using them for reading and/or writing to bytebuffers.

_octet_  works for both Clojure and Clojurescript, with the native buffer abstractions
appropriate bytebuffer abstractions for the specific platform:

- link:http://docs.oracle.com/javase/8/docs/api/java/nio/ByteBuffer.html[NIO ByteBuffer (clojure)]
- link:http://netty.io/4.1/api/io/netty/buffer/ByteBuf.html[Netty ByteBuf (clojure)]
- link:https://developer.mozilla.org/en/docs/Web/JavaScript/Typed_arrays[Typed Arrays ES6 (clojurescript)]


=== Defining a spec

A spec, in _octet_ parlance, represents a type definition or a composition of types.
Two most common composition types are: associative and indexed.

The difference of indexed and associative compositions is the input and output. In
associative composition the expected input and output is a map. And in indexed
composition, the expected input and input is a vector. The bytebuffer representation
is the same regardless.

Let start by defining one:

[source, clojure]
----
(require '[octet.core :as buf])

;; Indexed composition
(def my-spec1 (buf/spec buf/int32 buf/bool))

;; The same spec but using associative composition
(def my-spec2 (buf/spec :field1 buf/int32
                        :field2 buf/bool))
----

You can verify that the spec size (in bytes) and number of types internally is the
same for both:

.Example using `size` and `count` functions on specs
[source, clojure]
----
(buf/size my-spec1)
;; => 5

(count my-spec1)
;; => 2

(buf/size my-spec2)
;; => 5

(count my-spec2)
;; => 2
----


=== Creating a buffer

The next piece of the puzzle is to create (allocate) a new bytebuffer.
This operation is almost platform independent if the library defaults satisfies you.

.Example allocating a 24 bytes size byte buffer with *default* implementation
[source, clojure]
----
;; Allocate bytebuffer with 24 bytes of size
(def buffer (buf/allocate 24))
----

The buffer allocation is parameterisable so you can specify the concrete
implementation to use and the type of buffer:

.Example allocation 24 bytes size buffer with nio buffer implementation
[source, clojure]
----
;; This is a default if you are using clojure
(def buffer (buf/allocate 24 {:impl :nio :type :heap}))
----

It there are two types of buffers: `:heap` and `:direct`. The `:heap` type of buffer
uses the jdk/node heap for store the data and the `:direct` buffer type stores
the data out of the virtual machine heap. The main advantage of use `:direct`
buffers is that they are not affect the GC and may enable have less GC pauses.

The `:direct` type of buffers are only available on JDK.

Example allocating a 24 bytes size byte buffer using *es6typed arrays* implementation
[source, clojure]
----
;; This is a default if you are using clojurescript
(def buffer (buf/allocate 24 {:impl :es6 :type :heap}))
----

You can see all supported options <<supported-bytebuffers,here>>

[NOTE]
====
The return type of `allocate` depends on the implementation used. It is a plain
(native) object instance without any additional wrapping, which allows direct
manipulation via host interop.
====


=== Reading and writing data

It's time to see how we can write data to buffers and read data from them using
specs. Specs are a simple schema which describes how the data is read or written.

.Example writing data into buffer using indexed composed schema
[source, clojure]
----
;; The indexed composed spec expects a vector as input
(buf/write! buffer [22 true] my-spec1)
;; => 5
----

The `write!` function  returns a number of bytes are written into buffer.

As previously mentioned, indexed and associative specs with same fields (in same
order) represents the identical layout. Knowing that, we also can do the same
operation but using the associative spec defined previously:

.Example writing data into buffer using a map as input
[source, clojure]
----
(buf/write! buffer {:field1 22 :field2 true} my-spec2)
;; => 5
----

[NOTE]
====
Some buffer implementations (nio is an example) has the concept of read or
write position. _octet_ doesn't utilize that.
====

Secondly, the read operation is mostly similar to write. It reads from buffer
following the spec and returns the corresponding data structure:

.Example reading from buffer using indexed spec
[source, clojure]
----
(buf/read buffer my-spec1)
;; => [22 true]
----

Also, you can perform the same operation, but using a associative spec:

.Example reading from buffer using associative spec
[source, clojure]
----
(buf/read buffer my-spec2)
;; => {:field1 22 :field2 true}
----

[NOTE]
====
This works independently of implementation used for allocate the buffer. Some
implementations have a few limitations; es6 (cljs) as example, does not support
`int64` typespec due to platform limitations.
====

Composed type specs and plain value type specs implement the same abstraction, and
either can be used directly in read and write operations:

.Example using plain specs for read data from buffers
[source, clojure]
----
(buf/read buffer (buf/int16))
;; => 22
----


== Advanced usage

=== Read & Write with offset.

If you know that the data that you want to read is located at a specific location in
a buffer, you can specify it for a read or write operation:

.Example writing data in specific offset
[source, clojure]
----
(buf/write buffer [0 false] my-spec1 {:offset 20})
;; => [0 false]
----

.Example read data from specific offset.
[source, clojure]
----
(buf/read buffer my-spec1 {:offset 20})
;; => [0 false]
----


=== Show bytes read.

The default `read` function returns data read, but not the count of the bytes consumed.
For than, _octet_ exposes a convenience function `read*` instead returns 
a vector containing both the number of bytes read, and the data itself:
:

.Example using `read*` function
[source, clojure]
----
(buf/read* buffer my-spec2)
;; => [5 {:field1 22 :field2 true}]
----


=== Read & Write repeatedly

Sometimes you will want to read or write using a spec repeatedly, for that  _octet_
provides the `repeat` composition function:

.Example for read and write using repeat composed spec
[source, clojure]
----
(def spec (buf/repeat 5 buf/int32))
(buf/write buffer [1 2 3 4 5] spec)
;; => 20

(buf/read buffer spec)
;; => [1 2 3 4 5]
----


=== Using arbitrarily sized type specs

Until now, we have seen examples always using fixed size compositions. Fixed size
compositions are easy understand, and the size of the spec can be known at any time.
But in some circumstances, we may want to store arbitrary length data. Strings are one great
example:

.Example writing arbitrary length string into buffer
[source, clojure]
----
(buf/write! buffer "hello world" buf/string*)
;; => 15
----

.Example reading arbitrary length string from buffer
[source, clojure]
----
(buf/read buffer (buf/string*))
;; => "hello world"
----

This works because type specs like these are a implemented as a composition of two typespecs: *int32*
and fixed length *string*.  When writing, the string size can be determined, and written as `int32`, 
followed by the variable part of the string as `string`.  On read, the process is simply reveresed.

Note that the size of such type specs cannot be known ahead of time - because that is dependent 
on the actual run-time data involved:

.Example how obtain a size of specific type spec
[source, clojure]
----
(buf/size buf/int16)
;; => 2

(buf/size buf/string*)
;; => IllegalArgumentException No implementation of method: :size of protocol: #'octet.spec/ISpecSize found for class: octet.spec.string$string_STAR_$reify__1804  clojure.core/-cache-protocol-fn (core_deftype.clj:555)
----


=== Creating a new buffer from data
A helper function is provided to simplify the combination of operations required
to compute the required buffer size (from combining runtime data with arbitrarily sized specs),
allocating a buffer of the exact size required, and then populating it with the data.

.Example using `octet.core/into` function (semantically similar to clojure's `into`)
[source, clojure]
----
(def myspec (buf/spec buf/string* buf/string*))
(def buffer (buf/into myspec ["hello" "world!"]))

(buf/get-capacity buffer)
;; => 19

(buf/read buffer myspec)
;; => ["hello", "world!"]
----


=== Vectors

Similar to the repeating pattern, the vector abstraction allows an arbitrarily
sized repetion of a single spec, resulting in array-like data structures:

.Example storing two arrays in a buffer
[source, clojure]
----
(def spec (buf/spec
            (buf/vector* buf/int32)
            (buf/vector* buf/int32)))
(def buffer (buf/into spec [[1 2 3] [4 5 6 7 8]])

(buf/get-capacity buffer)
;; => 40

(buf/read buffer spec)
[[1 2 3] [4 5 6 7 8]]
----

Behind the scenes, a vector is represented as `int32 + type*N`, that means
that it has always an overhead of 4 bytes to store the length of the vector.


=== Read and write spec to multiple byte buffers

In some circumstances (specially when we working with streams) the buffers are
split. The simplest but not very efficient approach will be copy all data in one
unique byte buffer and read a spec from it. Octet comes with facilities for read a
spec from a vector of buffers that forgoes the need for copying data.

.Example reading and writing data to a vector of buffers
[source, clojure]
----
(def myspec (buf/spec buf/short buf/int32))

(def buffers [(buf/allocate 2)
              (buf/allocate 4)])

(buf/write! buffers [20 30] myspec)
;; => 6

(buf/read buffers spec)
;; => [20 30]

(buf/read (nth buffers 0) buf/short)
;; => 20

(buf/read (nth buffers 1) buf/int32)
;; => 30
----


=== Defining your own type spec

In some circumstances, you may wish to define your own typespecs to 
handle your concrete data structures.

 _octet_ is built around abstractions and defining a new typespec is
 a straightforward task.

An typespec consists mainly in `ISpec` protocol that has two methods: `read` and
`write`. For example, let's take an example of defining a typespec for point of coordinates:

.Example definition of type spec that represents a point
[source, clojure]
----
(require '[octet.spec :as spec])

;; Imagine you have a type Point defined like this:
(defrecord Point [x y])

;; Type spec definition for read/write Point instances.
(def point-spec
  (reify
    spec/ISpecSize
    (size [_]
      ;; we know that is datatype has fixed size in bytes
      ;; that represents two int32.
      8)

    spec/ISpec
    (read [_ buff pos]
      (let [[readed xvalue] (spec/read (buf/int32) buff pos)
            [readed' yvalue] (spec/read (buf/int32) buff (+ pos readed))]
        [(+ readed readed')
         (Point. xvalue yvalue)]))

    (write [_ buff pos point]
      (let [written (spec/write (buf/int32) buff pos (:x point))
            written' (spec/write (buf/int32) buff (+ pos written) (:y point))]
        (+ written written')))))
----

.Example using the previously defined typespec
[source, clojure]
----
(def mypoint (Point. 1 2))
(buf/write! buffer mypoint point-spec)
;; => 8

(buf/read* buffer point-spec)
;; => [8 #user.Point{:x 1, :y 2}]
----

You can also simplify the implementation through composition of existing typespecs 
using the *compose* function. The compose function is a type spec
constructor that helps map an indexed type spec to specific user defined type.

Let see how the previous code can be simplified in much less boilerplate code:

.Example using *compose* function.
[source, clojure]
----
(defrecord Point [x y])
(def mypoint (Point. 1 2))

(def point-spec (buf/compose ->Point [buf/int32 buf/int32]))

(buf/write! buffer mypoint point-spec)
;; => 8

(buf/read* buffer point-spec)
;; => [8 #user.Point{:x 1, :y 2}]
----


== Additional info

[[supported-bytebuffers]]
=== Supported byte buffers

This is a complete table of supported byte buffer implementations and type of
byte buffers:

[cols="1,2,2", options="header"]
|=========================================
| Platform | Name | Params
| Clojure | Heap NIO ByteBuffer | `{:type :heap :impl :nio}`
| Clojure | Direct NIO ByteBuffer | `{:type :direct :impl :nio}`
| Clojure | Heap Netty ByteBuf | `{:type :heap :impl :netty}`
| Clojure | Direct Netty ByteBuf | `{:type :direct :impl :netty}`
| ClojureScript | Heap ES6 ArrayBuffer/DataView | `{:type :heap :impl :es6}`
|=========================================


=== Supported typespecs

This is a complete list of supported plain value type spec:

[cols="1,1,1,2", options="header"]
|====================================================================
| Name    | Function      | Size (in bytes) | Notes
| Short   | `buf/int16`   | 2               |
| Integer | `buf/int32`   | 4               |
| Long    | `buf/int64`   | 8               | Only on jvm
| Float   | `buf/float`   | 4               |
| Double  | `buf/double`  | 8               |
| Boolean | `buf/bool`    | 1               |
| Byte    | `buf/byte`    | 1               |
| String  | `buf/string`  | N               | Fixed length string
| String  | `buf/string*` | 4+N             | Arbitrary length string
|====================================================================

Independently if a spec is a value spec or a composition of value specs, all them
implements the same abstraction and can be used in read or write operations.


=== Byte order

All the builtin implementations uses the `:big-endian` as default byte order. That
value can be cached at any time using the provided `*byteorder*` dynamic var on
the `octet.buffer` namespace.

Let see a little example:

[source, clojure]
----
(require '[octet.buffer])

(def myspec (buf/spec buf/string* buf/string*))

(def buffer
  (buf/with-byte-order :little-endian
    (buf/into myspec ["hello" "world!"])))

(buf/get-capacity buffer)
;; => 19

(buf/read buffer myspec)
;; => BufferUnderflowException (because of incorrect byte order)

(buf/with-byte-order :little-endian
  (buf/read buffer myspec))
;; => ["hello", "world!"]
----


== FAQ

*What is the difference with _clojurewerkz/buffy_?*

*Buffy* is a excellent library, and I have used it in some circumstances, but is has
some things that I personally don't like:

- It works only with netty bytebuf and I need an abstraction for work with
  different implementations, including in clojurescript.
- It has slightly strange and not uniform api when dynamic frames (arbitrary
  length size types) are used. _octet_ offers unified api for both type of specs.
- It wraps bytebuf in a self defined type. _octet_ is a lightweight abstraction
  that works over host implementations, without wrapping them.
- It not has support for ClojureScript


*What is the difference with _ztellman/gloss_?*

Gloss is also similar project, and has similar purposes, but it has several
differences:

- It has a limited set of types. Octet has an extensible abstraction for build own
  arbitrary type specs.
- It only works with nio as buffer implementations. Octet exposes an extensible
  abstraction and supports a few different implementations out of the box.
- In my opinion it has slightly ugly and unclear api.
- Seems not very maintained (has issues from 2013).
- It not has support for ClojureScript.


== How to Contribute?

=== Philosophy

Five most important rules:

- Beautiful is better than ugly.
- Explicit is better than implicit.
- Simple is better than complex.
- Complex is better than complicated.
- Readability counts.

All contributions to _octet_ should keep these important rules in mind.



=== Contributing

Unlike Clojure and other Clojure contributed libraries _octet_ does not have many
restrictions for contributions. Just open an issue or pull request.



=== Source Code

_octet_ is open source and can be found on
link:https://github.com/funcool/octet[github].

You can clone the public repository with this command:

[source,text]
----
git clone https://github.com/funcool/octet
----

=== Run tests

For running tests just execute this (for clojure):

[source, text]
----
lein test
----

And this for for clojurescript:

[source, text]
----
./scripts/build
node ./out/tests.js
----


=== License

_octet_ is licensed under BSD (2-Clause) license:

----
Copyright (c) 2015-2016 Andrey Antukh <niwi@niwi.nz>
All rights reserved.

Redistribution and use in source and binary forms, with or without
modification, are permitted provided that the following conditions are met:

* Redistributions of source code must retain the above copyright notice, this
  list of conditions and the following disclaimer.

* Redistributions in binary form must reproduce the above copyright notice,
  this list of conditions and the following disclaimer in the documentation
  and/or other materials provided with the distribution.

THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
----
